The examples in this section are based on the following sample graph:

[source,cypher]
----
MERGE (michael:Person {name: "Michael"})
WITH michael
CALL {
    WITH michael
    UNWIND range(0, 100) AS id
    MERGE (p:Person {name: "Person" + id})
    MERGE (michael)-[:KNOWS]-(p)
    RETURN count(*) AS friends
}

CALL {
    WITH michael
    UNWIND range(0, 50) AS id
    MERGE (p:Person {name: "Person" + id})
    MERGE (michael)-[:FOLLOWS]-(p)
    RETURN count(*) AS follows
}

RETURN friends, follows;
----

.Results
[opts="header"]
|===
| friends | follows
| 101 | 51
|===


[source,cypher]
----
MATCH (p1:Person {name: "Michael"})
MATCH (p2:Person {name: "Person60"})
RETURN apoc.nodes.connected(p1, p2) AS output;
----

.Results
[opts="header"]
|===
| output
| TRUE
|===

[source,cypher]
----
MATCH (p1:Person {name: "Michael"})
MATCH (p2:Person {name: "Person60"})
RETURN apoc.nodes.connected(p1, p2, "FOLLOWS") AS output;
----

.Results
[opts="header"]
|===
| output
| FALSE
|===

[source,cypher]
----
MATCH (p1:Person {name: "Michael"})
MATCH (p2:Person {name: "Person60"})
RETURN apoc.nodes.connected(p1, p2, "FOLLOWS>|KNOWS") AS output;
----

.Results
[opts="header"]
|===
| output
| TRUE
|===

[source,cypher]
----
MATCH (p1:Person {name: "Michael"})
MATCH (p2:Person {name: "Person30"})
RETURN apoc.nodes.connected(p1, p2, "FOLLOWS>") AS output;
----

.Results
[opts="header"]
|===
| output
| TRUE
|===